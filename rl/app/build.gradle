apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

def bakPath = file("${buildDir}/bakFile/")
def bakFiles = ["mapping.txt","resources.txt","seeds.txt","usage.txt"]
def ext_android = rootProject.ext.android
def led = rootProject.ext.led
//def deps = rootProject.ext.deps


//获取产品的名字
def getProductName() {
    return "led_service_"+ led.versionName + "${getJenkinsBuild()}"
}

//获取当前系统的时间
def releaseTime() {
    return new Date().format("yyyyMMddHHmmss")
}

boolean isInJeknins() {
    Map<String, String> map =  System.getenv()
    if(map == null) {
        return false
    }
//    printf('env=%s\n',map.toMapString())
    String str
    Iterator it = map.iterator();
    while(it.hasNext()) {
        str = it.next();
        if(str.contains("jenkins")){
            return true
        }
    }
    return false;
}
//获取jenkins build number
def getJenkinsBuild() {
    boolean  flag = isInJeknins();
    if(flag){
        ext.env = System.getenv()
        ext.buildNumber = env.get("BUILD_NUMBER");
        return "#" + "$buildNumber"
    }else{
        return ""
    }
}

def getProductProps(String product){
    def Properties props = new Properties()
    def proFile = file('products/'+ product +'/config.properties')
    if (proFile.canRead()){
        props.load(new FileInputStream(proFile))
    }
    props
}

android {
    compileSdkVersion ext_android.compileSdkVersion
    buildToolsVersion ext_android.buildToolsVersion
    defaultConfig {
        applicationId "com.ubtrobot.ledservice"
        minSdkVersion ext_android.minSdkVersion
        targetSdkVersion ext_android.targetSdkVersion
        versionCode led.versionCode
        versionName led.versionName
        multiDexEnabled  true
        ndk {
            abiFilters 'armeabi-v7a'
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("rk-debug.keystore")
            storePassword "ubt83474428"
            keyAlias "androiddebugkey"
            keyPassword "ubt83474428"
        }
        debug {
            storeFile file("rk-debug.keystore")
            storePassword "ubt83474428"
            keyAlias "androiddebugkey"
            keyPassword "ubt83474428"
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def oldFile = output.outputFile
            if (variant.buildType.name.equals('release')) {
                def releaseApkName = variant.flavorName + "_" + getProductName() + "_${releaseTime()}_"  + 'release.apk'
                output.outputFile = new File(oldFile.parent, releaseApkName)
            }
        }
    }

    android.applicationVariants.all{ variant->
        def taskName = variant.name
        tasks.all{
            if("assemble${taskName}".equalsIgnoreCase(it.name)){
                it.doLast{
                    copy{
                        def fileName = getProductName() + "_${releaseTime()}"
                        if(variant.productFlavors != null && variant.productFlavors[0] != null){
                            fileName += "_"+variant.productFlavors[0].name
                        }
                        if(variant.buildType != null){
                            fileName += "_"+variant.buildType.name
                        }
                        def destPath = bakPath;
                        def apkName = fileName+".apk"
                        def sourceApk = variant.outputs.outputFile
                        def sourceApkName = sourceApk[0].getName()
                        from sourceApk
                        into destPath

                        //备份 apk
                        include(sourceApkName)
                        rename(sourceApkName,apkName)

                        //备份混淆文件
                        from "${buildDir}/outputs/mapping/${variant.dirName}/"
                        into destPath
                        for(bakFileName in bakFiles){
                            include(bakFileName)
                            rename(bakFileName,fileName+"_"+bakFileName)
                        }
                    }
                }
            }
        }
    }

    buildTypes {
        release {
            manifestPlaceholders=["CATEGORY":"android.intent.category.DEFAULT"]
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            manifestPlaceholders=["CATEGORY":"android.intent.category.LAUNCHER"]
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignoreWarnings true
    }

    productFlavors {
        //对应产品名
        cruzr{
            def props = getProductProps('cruzr')
            if (props != null){
                applicationId props['applicationId']
                buildConfigField "String", "ILedContext", props['className']
                printf('applicationId=%s, className=%s\n',props['applicationId'], props['className'])
            }
        }
        alpha{
            def props = getProductProps('alpha')
            if(props!=null){
                applicationId props['applicationId']
                buildConfigField "String", "ILedContext", props['className']
                printf('applicationId=%s, className=%s\n',props['applicationId'], props['className'])
            }

        }
        alphamini{
            def props = getProductProps('alphamini')
            if(props!=null){
                applicationId props['applicationId']
                buildConfigField "String", "ILedContext", props['className']
                printf('applicationId=%s, className=%s\n',props['applicationId'], props['className'])
            }

        }

    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    def deps=rootProject.ext.deps;
    //测试
    testCompile deps.junit
    testCompile deps.mockito
    testCompile(deps.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    provided deps.supportAnnotations
//    compile deps.timber
    compile deps.servicebus
    compile deps.servicebusProtobufParam
    compile deps.utillib
    compile deps.ulog

    compile project(':alphaledservice')
    compile project(':ledcontext')
    alphaCompile project(':alpha')
    cruzrCompile project(':cruzr')
    alphaminiCompile project(':alphamini')
    compile project(path: ':ledsdk')
}
