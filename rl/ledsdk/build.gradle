apply plugin: 'com.android.library'


def deps = rootProject.ext.deps
def ext_android=rootProject.ext.android
def led=rootProject.ext.led

android {
    compileSdkVersion ext_android.compileSdkVersion
    buildToolsVersion ext_android.buildToolsVersion

    defaultConfig {
        minSdkVersion ext_android.minSdkVersion
        targetSdkVersion ext_android.targetSdkVersion
        versionCode led.versionCode
        versionName led.versionName

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile deps.servicebus
    compile deps.servicebusProtobufParam
    //    compile  (group: 'com.ubtrobot.servicebus', name: 'protobuf-param', version: '0.1.3')

    //changing snapshot to commit jar up maven it appears that compiler tell duplicated dependency of the protobufJava as below,so annotate it
    compile deps.protobufJava

    compile deps.supportAnnotations
    testCompile deps.junit
    compile project(':ledcontext')
}

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

def group = 'com.ubtrobot.lib.packets'
def libraryVersion = '0.0.9'
def baseName = project.getName()

task jarSdk(
        type: Jar,
        dependsOn: [
                ':ledprotos:assembleRelease',
                ':ledsdk:assembleRelease']) {
    archiveName = "${baseName}-${libraryVersion}.jar"

    //需打包的资源所在的路径集
    def srcClassDir = [project.buildDir.absolutePath + "/intermediates/classes/release",
                       project.buildDir.absolutePath + "/../../ledprotos/build/intermediates/classes/release" ];
    //初始化资源路径集
    from srcClassDir

    exclude "**/BuildConfig.class"
    exclude "**/R.class"
    exclude "**/BuildConfig\$*.class"
    exclude "**/R\$*.class"

    //只导入资源路径集下的部分资源
    include "com/ubtrobot/led/listener/*.class"
    include "com/ubtrobot/led/LedApi*.class"
    include "com/ubtrobot/led/protos/*.class"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId group
            artifactId baseName

            if (rootProject.ext.snapshot) {
                version = libraryVersion + "-SNAPSHOT"
            }else {
                version libraryVersion
            }
            artifact("build/libs/${baseName}-${libraryVersion}.jar")
        }
    }
}

artifactory {
    contextUrl = artifactory_url

    publish {
        repository {
            if (rootProject.ext.snapshot){
                repoKey = artifactory_rose_snapshot
            }else {
                repoKey = artifactory_rose_release
            }

            username = artifactory_username
            password = artifactory_password
        }

        defaults {
            publications('mavenJava')
            publishArtifacts = true
        }
    }

    resolve {
        repoKey = 'jcenter'
    }
}
